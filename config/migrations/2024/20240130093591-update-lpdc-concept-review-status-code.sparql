DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?o .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        VALUES ?s {
            <http://lblod.data.gift/concepts/3f2666df-1dae-4cc2-a8dc-e8213e713081>
        }
        ?s ?p ?o
    }
};


INSERT DATA {
    GRAPH <http://mu.semte.ch/graphs/public> {
        <http://lblod.data.gift/concepts/concept-status/gearchiveerd>
        a                                                   <http://www.w3.org/2004/02/skos/core#Concept> ;
        <http://mu.semte.ch/vocabularies/core/uuid>         """658b1835-29ec-4ea9-95ae-aaf1202ff9c8""" ;
        <http://www.w3.org/2004/02/skos/core#inScheme>      <http://lblod.data.gift/concept-schemes/cb0a8d0d-450e-4506-b9bf-d704e045d905>;
        <http://www.w3.org/2004/02/skos/core#prefLabel>     "Conceptual service instance was archived"@nl ;
        <http://www.w3.org/2004/02/skos/core#topConceptOf>  <http://lblod.data.gift/concept-schemes/cb0a8d0d-450e-4506-b9bf-d704e045d905> .
    }
};

DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?concept <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/concepts/3f2666df-1dae-4cc2-a8dc-e8213e713081> .
    }
}
INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?concept <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/concepts/concept-status/gearchiveerd> .
    }
}
WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?concept a <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#ConceptualPublicService> .
        ?concept <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/concepts/3f2666df-1dae-4cc2-a8dc-e8213e713081> .
    }
};
