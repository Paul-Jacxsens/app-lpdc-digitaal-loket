INSERT {
  GRAPH ?bestuurseenheidGraph {
    ?concept <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#hasConceptDisplayConfiguration> ?displayConfig .
    ?displayConfig a <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#ConceptDisplayConfiguration> ;
      <http://mu.semte.ch/vocabularies/core/uuid> ?configId ;
      <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#conceptIsNew> "true"^^<http://mu.semte.ch/vocabularies/typed-literals/boolean> ;
      <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#conceptInstantiated> "false"^^<http://mu.semte.ch/vocabularies/typed-literals/boolean> ;
      <http://purl.org/dc/terms/relation> ?bestuur .
  }
} WHERE {
  ?concept a <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#ConceptualPublicService> .
  ?concept <http://mu.semte.ch/vocabularies/ext/hasVersionedSource> ?versionedSource .

  ?bestuur a <http://data.vlaanderen.be/ns/besluit#Bestuurseenheid> ;
    <http://mu.semte.ch/vocabularies/core/uuid> ?bestuurId .

  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?bestuurId), "/LoketLB-LPDCGebruiker")) as ?bestuurseenheidGraph)
  BIND(SHA512(CONCAT(STR(?concept), STR(?bestuurId))) as ?configId)
  BIND(IRI(CONCAT('http://data.lblod.info/id/conceptual-display-configuration/', STR(?configId))) as ?displayConfig)

  FILTER NOT EXISTS {
    ?concept <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#hasConceptDisplayConfiguration> ?displayConfig .
    ?displayConfig <http://purl.org/dc/terms/relation> ?bestuur .
  }
}